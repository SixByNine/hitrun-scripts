#!/bin/tcsh -f
#
# killfile.csh: Searches for birdies and eliminates those channels
#               by running dedisperse_all in debird mode, seeking all
#               the .tim files and noting which channels have top snr>15
#               Outputs a killfile that can be used for dedispersing
# 
# Lina Levin 2008-11-10
if ( $# < 1 ) then
	echo "takes a fil file name"
	exit 1
endif

set filename = $1

if ( $# > 1 ) then
	set alwayskill=$2
endif

if ( -e $filename == 0 ) then
	echo "selected file '$filename' does not exist"
endif

set nch=`header $filename -nchans`
set thresh=15 # inital threashold for birdie zapping
set nsum=1
set start_ch = 0
set end_ch = $nch
if ( -e $HITRUN/config ) then
	source $HITRUN/config
endif
if ( ! ${?HITRUN_NTHREADS} ) then
	setenv HITRUN_NTHREADS 4
endif

set shredder = $HITRUN/bin/shredder
set multiseek_mod = $HITRUN/scripts/utilities/multiseek_killfile
set seek = $HITRUN/bin/seek
set header = $HITRUN/bin/header
set nthread = $HITRUN_NTHREADS

set date = /bin/date

#time stamp
echo "`du -h $filename`" > killtime.shredder.log
echo "Processing starting: "`$date` >> killtime.shredder.log

# Work out which 2^n to use...
set nsamp=`$header $filename -nsamples | awk -v nsum=$nsum '{printf "%d",$1/nsum}'`
echo $nsamp
set i=$nsamp
set pow2=0
while ( $i > 1 )
set pow2=`echo $pow2 | awk '{print $0 + 1}'`
set i=`echo $i | awk '{print int($i/2)}'`
end

set tlength=$pow2
echo "Using 2^$pow2 point transform"

rm -f debird.prd >&/dev/null

echo "Making single channel time-series with shredder"
set cmd = "${shredder} ${filename} -b ${start_ch} -e ${end_ch} -g 1000000"
echo $cmd
$cmd

#time stamp
echo "cmd = "$cmd >> killtime.shredder.log
echo "Debirding finished "`$date` >> killtime.shredder.log

echo "(multi)Seeking to find birdies"
echo ${multiseek_mod} ${seek} $nthread debird ${filename} ${tlength}
${multiseek_mod} ${seek} $nthread debird ${filename} ${tlength}

#time stamp
echo "cmd = ${multiseek_mod} ${seek} $nthread debird ${filename} ${tlength}" >> killtime.shredder.log
echo "Seek finished "`$date` >> killtime.shredder.log

rm -rf ch.list
set k = 0
while ( $k < $nch )
    echo $k | awk '{printf("%04d\n", $1)}' >>! ch.list
    @ k++
end

if ( -e $alwayskill ) then
	set nalways = `wc -w $alwayskill`
	echo "Killing $nalways channels by default"
	foreach channel (`cat $alwayskill`)
		set prdname=`echo $filename $channel | awk '{printf("%s.%04d.prd",$1,$2)}'`
		rm -f $prdname
	end
endif

rebird:
rm -rf ${filename}.killfile
rm -rf ${filename}.dspsrzap
foreach ch (`cat ch.list | awk '{print $1}'`)
    if ( -e ${filename}.${ch}.prd ) then
	#echo ${filename}.${ch}.prd 
	cat ${filename}.${ch}.prd | head -n 2 | tail -n 1 | awk -v thresh=$thresh '{if ($1>thresh) print "0"; else print "1"}' >>! ${filename}.killfile
	cat ${filename}.${ch}.prd | head -n 2 | tail -n 1 | awk -v thresh=$thresh -v ch=$ch '{if ($1>thresh)  print ch}' >>! ${filename}.dspsrzap
    else
	#echo $ch
	echo "0" >>! ${filename}.killfile
	echo $ch >>! ${filename}.dspsrzap
    endif
    
end


set nz=`grep 0 ${filename}.killfile | wc -l | awk '{print $1}'`
set loop=`echo "$nz $nch" | awk '{if (($1/$2) > 0.5) print "0"; else print "1"}'`
echo "$nch $nz $loop"
if ( $loop == 0 ) then
        set thresh=`echo $thresh | awk '{print $1 * 1.41}'`
        echo "More than 50% of chans zapped, could be a bad file or strong psr..."
        echo "Re-setting threshold to $thresh and trying again"
        goto rebird
endif
echo "Zapped $nz channels of $nch"


#time stamp
echo "Finished making killfile "`$date` >> killtime.shredder.log

rm -f ${filename}*.tim 
rm -f ${filename}*.prd
rm -f seeklist.*
rm -f ${filename}*.top
rm -f ${filename}*.log
rm -f ch.list

exit

